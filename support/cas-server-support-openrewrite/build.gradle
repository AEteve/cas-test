description = "Apereo CAS Open Rewrite Support"


tasks.register("createRecipe") {
    def casVersion = rootProject.version
    def tomcatVersion = project.tomcatVersion
    def sourceCompatibility = project.sourceCompatibility
    def springBootVersion = project.springBootVersion
    def jibVersion = project.jibVersion
    def gradleVersion = gradle.gradleVersion
    def recipeName = "cas${casVersion.replaceAll(/\.|-SNAPSHOT/, '')}"
    def templateContents = file("src/main/resources/current.gtemplate").text
    def recipeFileName = "$buildDir/generated/META-INF/rewrite/${recipeName}.yml"

    doFirst {
        def model = [:]
        model.put("recipeName", recipeName)
        model.put("tomcatVersion", tomcatVersion)
        model.put("casVersion", casVersion)
        model.put("javaVersion", sourceCompatibility)
        model.put("jibVersion", jibVersion)
        model.put("gradleVersion", gradleVersion)
        model.put("springBootVersion", springBootVersion)

        def engine = new groovy.text.GStringTemplateEngine()
        def recipe = engine.createTemplate(templateContents).make(model).toString()

        def outputFile = file(recipeFileName)
        outputFile.parentFile.mkdirs()
        outputFile.write(recipe)
    }
}

jar {
    dependsOn(createRecipe)
    from 'build/generated'
}
